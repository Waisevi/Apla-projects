contract VotingCandidateAccept {
    data {
        votingID int
        candidateID int
    }
    func closeNotification(){
        var id int
        id = Int(DBFind("notifications").Where({"recipient->member_id":$key_id, page_name:"voting_view", "page_params->voting_id":$votingID, closed:0}).One("id"))
        if id > 0 {
            NotificationsClose("notific_id", id)
        }
    }
    conditions {
        $WAITING = 1
        $VALID = 2
        $STARTED = 3
        $FINISHED = 4
        $INVALID = 5
        $voting = DBFind("votings").Columns("id,flags->decision,flags->notifics,status").WhereId($votingID).Row()
        if !$voting {
            warning "Voting is not found"
        }
        if $voting["status"] == $FINISHED {
            warning "Voting has expired. Voting is now not possible"
        }
        if $voting["status"] != $STARTED {
            warning "Voting has not yet begun. Try again later, please"
        }
        $subject_map = DBFind("votings_subject").Where({voting_id:$votingID, "subject->member_id":$candidateID}).Row()
        if !$subject_map {
            warning "Subject of voting is not found"
        }
        $participant_id = DBFind("votings_participants").Where({voting_id:$votingID, "member->member_id":$key_id}).One("id")
        if !$participant_id {
            warning "You are not a participant in this voting"
        }
    }

    action {
        if $voting["flags.notifics"] == 1 {
            closeNotification()
        }

        if $voting["flags.decision"] != 1 {
            var bt string
            bt = BlockTime()

            // write the result of the voting
            var m map
            m["decision_date"] = bt
            m["decision"] =  $candidateID
            DBUpdate("votings_participants", Int($participant_id), m)

            // increment count of accept
            DBUpdate("votings_subject", Int($subject_map["id"]), {"+number_accept":1})

            VotingUpdate("votingID", $votingID)
        }
    }
}