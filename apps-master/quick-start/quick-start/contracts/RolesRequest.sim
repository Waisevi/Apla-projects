contract RolesRequest {
    data {
        rid int
    }

    conditions {
        $role = DBFind("roles").Where("id=?", $rid).One("id")
        if !$role {
            warning "Role not found"
        }

        $assigned = DBFind("roles_participants").Where("role->id=? and member->member_id=? and deleted=0", $rid, $key_id).One("id")
        if $assigned {
            warning "You have already been added to the role"
        }

        $notifications = DBFind("notifications").Where("page_name=? and page_params->rid=? and sender->member_id=? and closed=0", "roles_request_view", $rid, $key_id).One("id")
        if $notifications {
            warning "Request already sent"
        }
    }
    
    action {
        var params map
        params["rid"] = $rid
        
        var member_id int
        member_id = EcosysParam("founder_account") 

        notifications_Send("member_id,sender,icon_name,text_header,text_body,page_name,params_map", 
            member_id, 1, "fa-bars", "Roles", "New request received", "roles_request_view", JSONEncode(params))
    }
}