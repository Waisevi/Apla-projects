contract catalog_ecosystems_Add {
    data {
        Ecosystem_name string
        Ecosystem_number int
        Ecosystem_type int
        Description string
        Conditions string
        Flag_free int
        Vde_form_address string "optional"
        Vde_page string "optional"
        Web_form_address string "optional"
        Web_page string "optional"
        Email string "optional"
        Phone string "optional"
    }
    conditions {
        var FOUNDER_ACCOUNT, DELEGATE_ACCOUNT string
        FOUNDER_ACCOUNT = "founder_account"
        DELEGATE_ACCOUNT = "delegate_account"
        
        if Size($Email) > 0 {
            var emailParts int
            emailParts = Split($Email, "@")
            if Len(emailParts) != 2 {
                warning "Invalid email"
            }
        }
        
        var ecoExists map
        ecoExists = DBFind("catalog_ecosystems").Where("ecosystem_number=? and deleted=0", $Ecosystem_number).Row()
        if ecoExists {
            info "The ecosystem with same number already added"
        }

        var isFounder, isDelegate bool
        var founder, delegate map
        founder = DBFind("parameters").Where("name=?", FOUNDER_ACCOUNT).Ecosystem($Ecosystem_number).Row()
        isFounder = founder["value"] == $key_id
        
        delegate = DBFind("parameters").Where("name=?", DELEGATE_ACCOUNT).Ecosystem($Ecosystem_number).Row()
        if delegate {
            isDelegate = delegate["value"] == $key_id
        }
        if !(isFounder || isDelegate) {
            warning "Sorry, you can not add this ecosystem"
        }
    }
    
    action {
        var ecosystem_info map
        ecosystem_info["description"] = $Description
        ecosystem_info["conditions"] = $Conditions
        ecosystem_info["email"] = $Email
        ecosystem_info["phone"] = $Phone
        
        var url_address map
        url_address["web_page"] = $Web_page
        url_address["web_form_address"] = $Web_form_address
        url_address["vde_page"] = $Vde_page
        url_address["vde_form_address"] = $Vde_form_address
        
        DBInsert("catalog_ecosystems", "ecosystem_name,ecosystem_number,ecosystem_type,image_id,ecosystem_info,url_address,flag_free",
        $Ecosystem_name, $Ecosystem_number, $Ecosystem_type, 0, ecosystem_info, url_address, $Flag_free)
    }
}