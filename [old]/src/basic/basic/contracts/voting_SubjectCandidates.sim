contract voting_SubjectCandidates {
    data {
        votingID int
        memberID int
    }
    func getMemberInfo(kid int) map {
        var res member map
        member = DBFind("members").Where("id=?", kid).Row()
        if member {
            res["member_id"] = member["id"]
            res["member_name"] = member["member_name"]
            res["image_id"] = member["image_id"]
        }else{
            member = DBFind("keys").Where("id=?", kid).Row()
            if member {
                res["member_id"] = member["id"]
                res["member_name"] = IdToAddress(Int(member["id"]))
                res["image_id"] = 0
            }
        }
        return res
    }
    func safeJsonToMap(m string) map {
        var res map
        if Size(m) > 0 { res = JSONToMap(m) }
        return res
    }

    conditions {
        $voting = DBFind("votings").Columns("id,date_started,optional,voting->type,flags->full_data").Where("id=? and date_started > ?", $votingID, BlockTime()).Row()
        if !$voting {
            warning "Voting already started. Can not edit document"
        }

        if $voting["flags.full_data"] == 1 && $voting["voting.type"] == 2 {
            warning "This vote is a system. Change settings not allowed"
        }

        if $memberID == 0 {
            $memberID = $key_id
        }

        if DBFind("votings_subject").Where("voting_id=? and subject->member_id=?", $votingID, $memberID).One("id") {
            warning "This candidature for this role has already been added before"
        }

        $member = getMemberInfo($memberID)
        if !$member {
            warning Sprintf("Key not found (%v)", $memberID)
        }
    }

    action {
        DBInsert("votings_subject", "voting_id,subject", $votingID, $member)

        var optional map
        optional = safeJsonToMap($voting["optional"])

        var number_candidates int
        if optional["number_candidates"]{
            number_candidates = Int(optional["number_candidates"])
            number_candidates = number_candidates + 1
        } else {
            number_candidates = 1
        }

        DBUpdate("votings", $votingID, "optional->number_candidates", number_candidates)
    }
}