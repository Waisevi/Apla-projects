contract business_Manager {
    data {
        Action string
        Id string "optional"
        EventId string "optional"
        ModelId string "optional"
        ObjectType string "optional"
        Title string "optional"
        RoleId string "optional"
        TextBody string "optional"
        NeedComment string "optional"
        // condition
        ConditionEventId array "optional"
        ConditionResult array "optional"
        ConditionDelete array "optional"
        // result
        Submit string "optional"
        SentBack string "optional"
        Accept string "optional"
        Escalation string "optional"
        Reject string "optional"
        Inform string "optional"
        Pay string "optional"
    }
    func boolChar(s string) int {
        if s {
            return 1
        }
        return 0
    }
    func safeGet(a array, i int) int {
        if Len(a) > i{
            return Int(a[i])
        }
        return 0
    }
    func safeGetBoolChar(a array, i int) int {
        if Len(a) > i{
            return boolChar(a[i])
        }
        return 0
    }
    func getResultId(r string)int{
        var i int
        while i<Len($results){
            if $results[i] == r{
                return i+1
            }
            i=i+1
        }
        return 0
    }
    func getCondArray(cond map) array {
        var res array
        // Int() for fixed "interface conversion: interface {} is nil, not decimal.Decimal"
        if Int(cond["EventId"]) > 0{
            res[0] = Sprintf("%v", cond["EventId"])
        }
        var type string
        var i int
        while i<Len($results){
            type = $results[i]
            if Int(cond[type]) == 1{
                // res[Len(res)] = Sprintf("%v(%v)", type, getResultId(type))
                res[Len(res)] = Sprintf("%v", type)
            }
            i=i+1
        }
        return res
    }
    conditions {
        $Id = Int($Id)
        $ApplicationId = Int(DBFind("applications").Where("name='Business'").One("id"))
        if $ApplicationId == 0 {
            warning "Application ID not found"
        }
        $results = AppParam($ApplicationId, "results")
        $results = Split($results, ",")

        $SUBMIT = "Submit"
        $SENDBACK = "Send Back"
        $ACCEPT = "Accept"
        $ESCALATION = "Escalation"
        $REJECT = "Reject"
        $INFORM = "Inform"
        $PAY = "Pay"
    }
    action {
        if $Action == "create" || $Action == "update"{
            $ModelId = Int($ModelId)
            $ObjectType = Int($ObjectType)
            $RoleId = Int($RoleId)
            $EventId = Int($EventId)
            $NeedComment = boolChar($NeedComment)
            var condition map
            var conditionPretty array
            var i int
            while i<Len($ConditionResult){
                var conditionDelete int
                conditionDelete = safeGetBoolChar($ConditionDelete, i)
                if !conditionDelete {
                    var cond map
                    var key val int
                    key = safeGet($ConditionEventId, i)
                    val = safeGet($ConditionResult, i)
                    condition[Str(key)] = val
                    var prettyPrint string
                    if key == 0{
                        // prettyPrint = Sprintf("%v(%v)", $results[val-1], val)
                        prettyPrint = Sprintf("%v", $results[val-1])
                    }else{
                        // prettyPrint = Sprintf("[%v:%v(%v)]", key, $results[val-1], val)
                        prettyPrint = Sprintf("[%v:%v]", key, $results[val-1])
                    }
                    conditionPretty[Len(conditionPretty)] = prettyPrint
                }
                i=i+1
            }
            $Condition = JSONEncode(condition)
            $conditionPretty = Join(conditionPretty, " OR ")

            var result map
            result[$SUBMIT] = boolChar($Submit)
            result[$SENDBACK] = boolChar($SentBack)
            result[$ACCEPT] = boolChar($Accept)
            result[$ESCALATION] = boolChar($Escalation)
            result[$REJECT] = boolChar($Reject)
            result[$INFORM] = boolChar($Inform)
            result[$PAY] = boolChar($Pay)
            $Result = JSONEncode(result)
            $resultPretty = Join(getCondArray(result), ", ")
        }

        if $Action == "create"{
            DBInsert("business_models", "event_id,object_type,title,role_id,text_body,need_comment,condition,result,condition_pretty,result_pretty,model_id", $EventId,$ObjectType,$Title,$RoleId,$TextBody,$NeedComment,$Condition,$Result,$conditionPretty,$resultPretty,$ModelId)
        }
        if $Id == 0 {
            return
        }
        if $Action == "update" {
            DBUpdate("business_models", $Id, "event_id,object_type,title,role_id,text_body,need_comment,condition,result,condition_pretty,result_pretty,model_id", $EventId,$ObjectType,$Title,$RoleId,$TextBody,$NeedComment,$Condition,$Result,$conditionPretty,$resultPretty,$ModelId)
        }
        if $Action == "delete" {
            DBUpdate("business_models", $Id, "deleted", 1)
        }
        if $Action == "restore" {
            DBUpdate("business_models", $Id, "deleted", 0)
        }
    }
}