contract VotingNotificationSend {
    data {
        votingID int
    }
    func safeJsonToMap(m string) map {
        var res map
        if Size(m) > 0 {
            res = JSONToMap(m)
        }
        return res
    }
    conditions {
        var bt string
        bt = BlockTime()
        $voting = DBFind("votings").Columns("id,date_ended").Where({id:$votingID, date_ended:[{"$gt":bt}]}).One("id")
        if !$voting {
            warning "Voting has expired. Notifications can not be sent"
        }

        $voting = DBFind("votings").Columns("voting->name,creator->member_id,flags->notifics,flags,date_started").Where({id:$votingID, date_started:[{"$gt":bt}]}).Row()
        if !$voting {
            warning "Voting has not yet begun. Try again later, please"
        }
        if $voting["creator.member_id"] != $key_id {
            warning "You are not the creator of the voting"
        }
        if $voting["flags.notifics"] == 1 {
            warning "Notifications have already been sent"
        }

        $voting_name = $voting["voting.name"]
    }

    action {
        var participants array
        participants = DBFind("votings_participants").Where({voting_id:$votingID, decision:0})
        var i int
        while i < Len(participants) {
            var participant member params map
            participant = participants[i]
            member = safeJsonToMap(participant["member"])
            params["voting_id"] = $votingID
            NotificationsSend("member_id,sender,icon_name,text_header,text_body,page_name,params_map,rid,closure_type", Int(member["member_id"]), 1, "fa-check", "Voting", $voting_name, "voting_view", JSONEncode(params), 0, 0)

            i = i + 1
        }

        DBUpdate("votings", $votingID, {"flags->notifics":1})
    }
}