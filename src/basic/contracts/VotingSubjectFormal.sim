contract VotingSubjectFormal {
    data {
        votingID int

        description string "optional"
        table_to_written string
        row_id string
        column_name string
        written_value string

        contract_accept string "optional"
        contract_accept_params string "optional"
        contract_reject string "optional"
        contract_reject_params string "optional"
    }

    func safeJsonToMap(m string) map {
        var res map
        if Size(m) > 0 { res = JSONToMap(m) }
        return res
    }

    conditions {
        $STARTED = 3
        $FINISHED = 4
        $voting = DBFind("votings").Where({id:$votingID, status:[{"$neq":$STARTED},{"$neq":$FINISHED}]}).Columns("id,date_started,flags->full_data,voting->type,optional").Row()
        if !$voting {
            warning "Voting already started. Can not edit document"
        }

        if $voting["flags.full_data"] == 1 && $voting["voting.type"] == 2 {
            warning "This vote is a system. Change settings not allowed"
        }
    }

    action {
        var subject map
        subject = {description:$description, table:$table_to_written, table_id:$row_id, column:$column_name, column_value:$written_value}

        $subject_id = DBFind("votings_subject").Where({voting_id:$votingID}).One("id")
        if !$subject_id {
            DBInsert("votings_subject", {voting_id:$votingID, subject:subject})
        } else {
            DBUpdate("votings_subject", Int($subject_id), {subject:subject})
        }

        var accept_params reject_params map
        accept_params = safeJsonToMap($contract_accept_params)
        reject_params = safeJsonToMap($contract_reject_params)

        var optional map
        optional = safeJsonToMap($voting["optional"])
        optional["contract_accept"] = $contract_accept
        optional["contract_accept_params"] = accept_params
        optional["contract_reject"] = $contract_reject
        optional["contract_reject_params"] = reject_params

        DBUpdate("votings", $votingID, {optional:optional})

        VotingSubjectCheck("votingID", $votingID)
    }
}