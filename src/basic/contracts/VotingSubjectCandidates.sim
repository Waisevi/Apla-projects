contract VotingSubjectCandidates {
    data {
        votingID int
        memberID int
    }
    func getMemberInfo(kid int) map {
        var res member map
        member = DBFind("members").WhereId(kid).Row()
        if member {
            res["member_id"] = member["id"]
            res["member_name"] = member["member_name"]
            res["image_id"] = member["image_id"]
        }else{
            member = DBFind("keys").WhereId(kid).Row()
            if member {
                res["member_id"] = member["id"]
                res["member_name"] = IdToAddress(Int(member["id"]))
                res["image_id"] = 0
            }
        }
        return res
    }

    conditions {
        $STARTED = 3
        $FINISHED = 4        
        $voting = DBFind("votings").Where({id:$votingID}).Columns("id,optional->number_candidates,voting->type,flags->full_data,status").Row()
        if $voting["status"] == $STARTED {
            warning "Voting already started. Can not add candidates"
        }
        if $voting["status"] == $FINISHED {
            warning "Voting already finished. Can not add candidates"
        }

        if $voting["flags.full_data"] == 1 && $voting["voting.type"] == 2 {
            warning "This vote is a system. Change settings not allowed"
        }

        if $memberID == 0 {
            $memberID = $key_id
        }

        if DBFind("votings_subject").Where({voting_id:$votingID, "subject->member_id":$memberID}).One("id") {
            warning "This candidature for this role has already been added before"
        }

        $member = getMemberInfo($memberID)
        if !$member {
            warning Sprintf("Key not found (%v)", $memberID)
        }
    }

    action {
        DBInsert("votings_subject", {voting_id:$votingID, subject:$member})
        var m map
        m["optional->number_candidates"] = Int($voting["optional.number_candidates"]) + 1
        DBUpdate("votings", $votingID, m)
    }
}