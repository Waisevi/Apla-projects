contract EditParameter {
    data {
        Id int
        Value string "optional"
        Conditions string "optional"
    }
    func onlyConditions() bool {
        return $Conditions && !$Value
    }

    conditions {
        RowConditions("parameters", $Id, onlyConditions())
        if $Conditions {
            ValidateCondition($Conditions, $ecosystem_id)
        }
		$Name = DBFind("parameters").WhereId($Id).One("name")
		if $Name == "founder_account" {
			if !DBFind("members").Where({id:$Value}).One("id") {
				 warning Sprintf("User %v not found", $Value)
			}
		}
        if $Name == "max_block_user_tx" || $Name == "money_digit" || $Name == "max_sum" || $Name == "min_page_validate_count" || $Name == "max_page_validate_count" {
			if Size($Value) == 0 {
				warning "Value was not received"
			}
            if Int($Value) <= 0 {
				warning "Value must be greater than zero"
			}
        }
    }

    action {
        var pars map
        if $Value {
            pars["value"] = $Value
        }
        if $Conditions {
            pars["conditions"] = $Conditions
        }
        if pars {
            DBUpdate("parameters", $Id, pars)
        }
    }
}
